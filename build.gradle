plugins {
    id "me.dreamhopping.pml.gradle" version "3.12.1+build.26"
    id "org.bookmc.annotation-processor-configuration" version "0.2.0+build.5"
}

allprojects {
    apply plugin: "java"
    apply plugin: "me.dreamhopping.pml.gradle"
    apply plugin: "org.bookmc.annotation-processor-configuration"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(16)
        }
    }

    group "org.bookmc"
    version "0.1.0"

    sourceSets {
        // We create a "virtual" sourceset as this allow for access to the APIs
        // without exposure to version-specific code. In other words this could
        // does not require you to use it in the target-version's sourceset
        // but can be used from the main sourceset.
        virtual {}
    }

    repositories {
        mavenCentral()
        maven {
            name "BooKMC"
            url "https://maven.bookmc.org/releases/"
        }

        maven {
            name "FabricMC"
            url "https://maven.fabricmc.net"
        }
    }


    dependencies {
        compileOnly "org.apache.logging.log4j:log4j-api:2.0-beta9"
        implementation "org.bookmc:book-loader:1.0.3"
        remapImplementation "org.bookmc:minecraft:0.22.0"

        compileOnly sourceSets.virtual.runtimeClasspath
    }

    processResources {
        inputs.property "version", project.version

        filesMatching("book.mod.json") {
            expand "version": project.version
        }
    }

    minecraft {
        separateVersionJars()

        target "1.17.1", "1.8.9"
    }
}

subprojects {
    dependencies {
        implementation project(":")
    }
}